     1	From: fdeck@sleepy.helios.nd.edu (francis deck)
     2	Subject: CHEAP 8-BIT ADC FOR IBM PC
     3	
     4	PLANS FOR A 4-CHANNEL 8-BIT ANALOG-TO-DIGITAL CONVERTER FOR PC
     5	
     6	                        Francis J. Deck
     7	                   fdeck@grumpy.helios.nd.edu
     8	
     9	This is an extremely simple ADC for the PC.  It connects to the parallel 
    10	printer port, and runs from a 9V battery.  All parts are available from 
    11	Digi-Key Corporation (1-800-DIGI-KEY), and cost is under $20, including box!  
    12	Input voltage range is 0 to 5 V.  Driver software is in Turbo Pascal.  I 
    13	measured the speed to be roughly 1200 samples/sec on an 8 MHz XT, and 5700/sec 
    14	on the same PC with a 10 MHz 80286/cache accelerator card turned on.
    15	
    16	You'll see that this circuit doesn't require a bidirectional 8-bit i/o port.  
    17	It should work even on the most wild of clones.  I've had it going on IBM, 
    18	Zenith, Jameco, etc.
    19	
    20	Needless to say, this is not an extreme precision device, but should suffice
    21	for many medium-speed application, such as temperature logging or alarms.  An 
    22	LM34 temperature sensor (10 mV/F output) would allow 2 degree resolution, for 
    23	instance.  For signals which are noisy beyond 1 LSB (roughly 20 mV), taking 
    24	several readings and averaging them will improve the effective resolution by 
    25	the square root of the number of readings.
    26	
    27	SCHEMATIC DIAGRAM:
    28	          ________________
    29	       + |                |
    30	       __|__              |
    31	        ___   9 V         |
    32	       _____  battery     |
    33	        ___               |
    34	         |                |
    35	         |                |
    36	       __|__             <
    37	        ___               >    1 K Resistor
    38	         _               <
    39	                          >                    Parallel Printer Port:
    40	                          |                    (pin numbers shown are
    41	                          | 1                  for DB25 male plug)
    42	                   _______|_________
    43	                  |      V+         | 12           2
    44	                  |             clk |----------------o D0
    45	Inputs:           |                 | 2            3
    46	                3 |             cs' |----------------o D1
    47	    A0 o----------| in0             | 13           4
    48	                4 |             din |----------------o D2
    49	    A1 o----------| in1             | 10          10
    50	                5 |            dout |----------------o Acknowledge
    51	    A2 o----------| in2             | 7           18
    52	                6 |            dgnd |----------------o Ground
    53	    A3 o----------| in3             |         |
    54	                8 |                 |         |
    55	   Gnd o----------| agnd            |       __|__
    56	                  |                 |        ___
    57	                  |  ADC0833CCN     |         _
    58	                  |  A to D Conv.   |
    59	                  |                 |
    60	                  | vcc     vref/2  |
    61	                  |_________________|
    62	                     | 7       | 9
    63	                     |         |
    64	                     |         | +2.5 V
    65	                   __|__       |
    66	           0.1 uF  _____       |
    67	           Disc      |         |
    68	           Capacitor |         | +
    69	                     |     ____|___|
    70	                     |     |  / \
    71	                     |       /   \      LM336Z-2.5
    72	                     |      /_____\     Precision Reference IC
    73	                     |         |
    74	                     |         | -
    75	                     ----------|
    76	                             __|__
    77	                              ___
    78	                               _
    79	
    80	Note:  The pins of the LM336 can be identified as follows:  The (+) terminal is 
    81	the middle pin.  Now, look at the lettering on the chip, while holding the chip 
    82	with pins down and lettering facing you, the (-) terminal is on the right.
    83	
    84	Some notes:  First, V+ is the input to an internal Zener regulator, and power 
    85	supplies ranging from +9 to +15 V can be used.  This makes it convenient to 
    86	mount the circuit inside an existing powered device, such as a light meter.  
    87	The circuit can also be powered by eliminating the battery and resistor, and 
    88	connecting a +5 V supply to the Vcc terminal. 
    89	
    90	I made a tiny little printed circuit board for this device, but it's hardly 
    91	necessary, considering the low component count.  A little piece of perfboard 
    92	will suffice.  Don't use cables longer than 1m to connect to the PC, since 
    93	raw TTL isn't very good for long-haul data transmission.
    94	
    95	PARTS LIST [fmg - prices may have changed since the time this was posted!]
    96	
    97	All the parts come from Digi-Key Corp, 1-800-DIGI-KEY.
    98	
    99	        Component       Part #          Price
   100	        ---------       ------          -----
   101	        ADC0833         ADC0833CCN      $5.00
   102	        LM336-2.5       LM336Z-2.5      1.05
   103	        1k ohm          1.0KQ           .26/5
   104	        0.1 uF          P4311-ND        1.70/10
   105	        DB25 male plug  225M-ND         1.08
   106	        14-pin IC skt.  A9314           .29
   107	        9 V batt. snap  BS6I-ND         .22
   108	        9 V batt.       P104            1.04
   109	        ----------------------------------------
   110	                        TOTAL:          10.64
   111	
   112	DRIVER SOFTWARE LISTING
   113	
   114	These codes are all written in Turbo Pascal.
   115	
   116	{-------- ADC driver ANALOG.PAS -------}
   117	
   118	unit analog;
   119	
   120	interface
   121	
   122	{-------- User-modifyable constants ------}
   123	
   124	const
   125	     nlpt = 1; {printer port number}
   126	     vref = 2.5; {reference voltage for ADC}
   127	
   128	{-------- Read an ADC channel --------}
   129	
   130	function analog_read (ch: integer): real;
   131	
   132	implementation
   133	
   134	var
   135	   in_addr, out_addr: word; {i/o addresses}
   136	
   137	const
   138	     cs = 2; {ADC chip select line}
   139	     di = 4; {ADC data input line}
   140	     clk = 1; {ADC clock line}
   141	     dout = 64; {ADC data output line}
   142	
   143	     a: array[0..3] of byte = {Channel select word, bit #1}
   144	        (0,di,0,di);
   145	
   146	     b: array[0..3] of byte = {Channel select word, bit #2}
   147	        (0,0,di,di);
   148	
   149	{-------- Read an ADC channel --------}
   150	
   151	function analog_read (ch: integer): real;
   152	
   153	var
   154	   result: byte;
   155	   i: integer;
   156	
   157	begin
   158	     {transmit chip setup sequence as per ADC0833 data sheet}
   159	
   160	     port[out_addr] := 0;
   161	     port[out_addr] := di;
   162	     port[out_addr] := di + clk;
   163	     port[out_addr] := di;
   164	     port[out_addr] := di + clk;
   165	     port[out_addr] := di;
   166	     port[out_addr] := a[ch];
   167	     port[out_addr] := a[ch] + clk;
   168	     port[out_addr] := b[ch];
   169	     port[out_addr] := b[ch] + clk;
   170	     port[out_addr] := di;
   171	     port[out_addr] := di + clk;
   172	     port[out_addr] := di;
   173	     port[out_addr] := di + clk;
   174	     port[out_addr] := 0;
   175	
   176	     {the main conversion loop}
   177	
   178	     result := 0;
   179	     for i := 1 to 8 do begin
   180	         result := result*2;
   181	         port[out_addr] := clk;
   182	         if port[in_addr] and dout <> 0 then result := result + 1;
   183	         port[out_addr] := 0;
   184	         end;
   185	     port[out_addr] := cs;
   186	
   187	     analog_read := result*vref*7.8125e-3;
   188	     end;
   189	
   190	{-------- Initialization Section --------}
   191	
   192	const
   193	     base: word = $40;
   194	     offs: array[1..3] of word = ($08,$0a,$0c);
   195	     dummy: real;
   196	
   197	begin
   198	     {find out where printer port is}
   199	
   200	     out_addr := memw[base:offs[nlpt]];
   201	     in_addr := out_addr + 1;
   202	
   203	     {place ADC in idling mode}
   204	
   205	     port[out_addr] := cs;
   206	
   207	     {read ADC a few times to let it settle}
   208	
   209	     dummy := analog_read(0);
   210	
   211	     end.
   212	
   213	{-------- End of ANALOG.PAS ---------}
   214	
   215	{-------- Test program TEST.PAS --------}
   216	
   217	program test;
   218	
   219	uses
   220	    crt,
   221	    analog;
   222	
   223	var
   224	   i: integer;
   225	
   226	begin
   227	     while not keypressed do begin
   228	           for i := 0 to 3 do write (analog_read(i):3:3,' ');
   229	           writeln;
   230	           delay (500);
   231	           end;
   232	     end.
   233	
   234	{-------- End of TEST.PAS --------}
   235	
   236	If there is suitable interest in this topic, measured by how much e-mail y'all 
   237	send me, I will continue to post simple circuits.  In the future, I may have 
   238	to switch to Postscript or HP-GL format for the schematics, which I'd mail to 
   239	interested persons.
   240	
   241	[fmg - the above started a heated debate on the format of posted schematics -
   242	       the result of which seems to have been loss of interested in posting
   243	       them... bummer]
   244	
   245	Incidentally, there's a real *gold mine* of inexpensive ICs out there which do 
   246	all sorts of things, and which, like the ADC0833 above, use *synchronous 
   247	serial i/o*, meaning that the send and receive data one bit at a time under 
   248	control of an external clock signal.  This kind of i/o is very conducive to 
   249	parallel port connection, as I've shown.  I will soon post a list of such 
   250	chips which range from ADC and DACs to remote digital peripherals to EEPROMs 
   251	to graphic equalizers!
   252	 